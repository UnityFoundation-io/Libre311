// Copyright 2023 Libre311 Authors
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

plugins {
    id("com.github.johnrengelman.shadow") version "7.1.2"
    id("io.micronaut.application") version "3.7.10"
    id("io.micronaut.test-resources") version "3.7.10"
}

version = "0.1"
group = "app"

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor("io.micronaut.data:micronaut-data-processor")
    annotationProcessor("io.micronaut:micronaut-http-validation")
    annotationProcessor("io.micronaut.security:micronaut-security-annotations")
    annotationProcessor("io.micronaut.openapi:micronaut-openapi")
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut:micronaut-jackson-databind")
    implementation("jakarta.annotation:jakarta.annotation-api")
    runtimeOnly("ch.qos.logback:logback-classic")
    implementation("io.micronaut:micronaut-validation")
    implementation("io.swagger.core.v3:swagger-annotations")

    implementation("io.micronaut.xml:micronaut-jackson-xml")
    implementation("io.micronaut.data:micronaut-data-hibernate-jpa")
    implementation("io.micronaut.sql:micronaut-jdbc-tomcat")
    implementation("io.micronaut.objectstorage:micronaut-object-storage-gcp")
    implementation("io.micronaut.security:micronaut-security-jwt")
    implementation('com.opencsv:opencsv:5.7.1')
    implementation("io.micronaut.reactor:micronaut-reactor")
    implementation("io.micronaut.flyway:micronaut-flyway")

    String databaseSelection = System.getenv("LIBRE311_DATABASE_DEPENDENCY")

    if (databaseSelection != null) {
        databaseSelection.split(',').each {part ->
            runtimeOnly(part)
        }
    } else {
        runtimeOnly("com.h2database:h2")
    }

    // apply database specific dependency for flyway.  None needed for H2.
    String databaseType = System.getenv("LIBRE311_DB_TYPE")
    if (databaseType == "mysql"){
        runtimeOnly("org.flywaydb:flyway-mysql")
    }
    else if (databaseType == "postgresql"){
        runtimeOnly("org.flywaydb:flyway-database-postgresql")
    }
}

application {
    mainClass.set("app.Application")
}
java {
    sourceCompatibility = JavaVersion.toVersion("11")
    targetCompatibility = JavaVersion.toVersion("11")
}

graalvmNative.toolchainDetection = false
micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("app.*")
    }
    // see https://micronaut-projects.github.io/micronaut-gradle-plugin/latest/#_configuring_the_test_resources_plugin
    testResources {
        enabled = true
    }
}

tasks.withType(JavaCompile) {
    options.fork = true
    options.forkOptions.jvmArgs << '-Dmicronaut.openapi.views.spec=rapidoc.enabled=true,swagger-ui.enabled=true,swagger-ui.theme=flattop'
}
